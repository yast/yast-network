/**
 * File:	modules/NetworkStorage.ycp
 * Package:	Network configuration
 * Summary:	Networked disks
 * Authors:	Martin Vidner <mvidner@suse.cz>
 *
 * $Id: NetworkStorage.ycp 38154 2007-05-28 08:20:39Z mzugec $
 *
 * #176804 - Root on iSCSI installation fails
 */

{

module "NetworkStorage";

import "Sh";
import "Storage";

/**
 * Ask /proc/mounts what device a mount point is using.
 * @return e.g. /dev/sda2 (or just "nfs")
 */
global string getDevice(string mount_point) {
    string cmd = sformat("grep ' %1 ' /proc/mounts|grep -v rootfs", mount_point);
    list<string> fields = Sh::Words(cmd);
    string vfstype = fields[2]:"";
    string device = (vfstype == "nfs" || vfstype == "nfs4")? "nfs": fields[0]:"";
    y2milestone("%1 is on device: %2", mount_point, device);
    return device;
}

/**
 * If the disk is on a networked device (NFS, ISCSI),
 * the main NIC needs STARTMODE nfsroot instead of auto.
 * @return root dev over network: `no `iscsi `nfs `fcoe
 */
global symbol isDiskOnNetwork(string device) {
    return Storage::IsDeviceOnNetwork(device);
}

global list<string> getiBFTDevices() {
  if (Sh::OK("ls /sys/firmware/ibft")){
        return Sh::Lines("ls /sys/firmware/ibft/ethernet*/device/net/");
    }
    else {
        return [];
    }
}

/* EOF */
}
