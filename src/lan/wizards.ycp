/****************************************************************************
 *
 * Copyright (c) 2012 Novell, Inc.
 * All Rights Reserved.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of version 2 of the GNU General Public License as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail,
 * you may find current contact information at www.novell.com
 *
 ****************************************************************************/
/**
 * File:	include/network/lan/wizards.ycp
 * Package:	Network configuration
 * Summary:	Network cards configuration wizards
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 */

{

textdomain "network";

import "Arch";
import "Label";
import "Lan";
import "Sequencer";
import "Wizard";

include "network/routines.ycp";
include "network/lan/address.ycp";
include "network/lan/complex.ycp";
include "network/lan/dhcp.ycp";
include "network/lan/hardware.ycp";
include "network/lan/wireless.ycp";
include "network/services/dns.ycp";
include "network/services/host.ycp";

define symbol NetworkCardSequence(string action);
define symbol MainSequence(string mode);
define symbol AddressSequence(string which);

/**
 * Whole configuration of network
 * @return successfully finished
 */
symbol LanSequence() {

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence("") ),
	"packages"	: [ ``( PackagesInstall(Lan::Packages()) ), true ],
	"write"	: [ ``( WriteDialog() ), true ],
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "packages",
	],
	"packages" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next,
	]
    ];

    Wizard::OpenCancelOKDialog();
    Wizard::SetDesktopTitleAndIcon("lan");

    symbol ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of network but without reading and writing.
 * For use with autoinstallation and proposal
 * @param mode if "proposal", NM dialog may be skipped
 * @return sequence result
 */
define symbol LanAutoSequence (string mode) ``{
    string caption = _("Network Card Configuration");
    term contents = `Label(_("Initializing..."));

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("lan");
    Wizard::SetContentsButtons(caption, contents, "",
				Label::BackButton(), Label::NextButton());

    symbol ret = MainSequence (mode);

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the network configuration
 * @param mode if "proposal", NM dialog may be skipped
 * @return sequence result
 */
define symbol MainSequence (string mode) ``{

    map aliases = $[
	"global"	: ``(MainDialog ("global")),
	"overview"	: ``(MainDialog ("overview")),
	"add"		: [ ``(NetworkCardSequence("add")), true ],
	"edit"		: [ ``(NetworkCardSequence("edit")), true ],
	"init_s390"	: [ ``(NetworkCardSequence("init_s390")), true ],
    ];

    string start = "overview";
    // the NM decision is already present in the proposal.
    // see also #148485
    if (mode == "proposal") start = "global";
    map sequence = $[
	"ws_start" : start,
	"global" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
	    ],
	"overview" : $[
	    `abort	: `abort,
	    `next	: `next,
	    `add	: "add",
	    `edit	: "edit",
            `init_s390  : "init_s390",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
        "init_s390" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
    ];

    return Sequencer::Run(aliases, sequence);
}

/**
 * Configuration sequence
 * @param action which workflow ("add"|"edit")
 * @return sequence result
 */
define symbol NetworkCardSequence(string action) ``{
    map aliases = $[
	"hardware"	: ``(HardwareDialog()),
	"address"	: ``(AddressSequence("")),
	"s390"		: ``(S390Dialog()),
    ];

    string ws_start = "address";

    switch( action)
    {
        case "add":
            ws_start = "hardware";
            break;

        case "edit":
            ws_start = "address";
            break;

        case "init_s390":
            // s390 may require configuring additional modules. Which
            // enables IBM net cards for linux. Basicaly it creates 
            // linux devices with common api (e.g. eth0, hsi1, ...)
            ws_start = "s390";
            break;
    }

    y2milestone("ws_start %1", ws_start);

    map sequence = $[
	"ws_start" : ws_start,
	"hardware" : $[
	    `abort	: `back,
	    `next	: "address",
	],
	"address" : $[
	    `abort	: `back,
	    `next	: `next,
	],
	"s390" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
    ];

    return Sequencer::Run(aliases, sequence);
}

/**
 * Configuration sequence
 * @param which: if "wire", go directly to wifi setup
 * @return sequence result
 */
define symbol AddressSequence(string which) ``{

    map aliases = $[
//	"changedefaults": [ ``(ChangeDefaults()), true ],
	"address"	: ``(AddressDialog()),
	"hosts"		: ``(HostsMainDialog(false)),
	"s390"		: ``(S390Dialog()),
	"wire"		: ``(WirelessDialog()),
	"expert"	: ``(WirelessExpertDialog()),
	"keys"		: ``(WirelessKeysDialog()),
	"eap"		: ``(WirelessWpaEapDialog()),
	"eap-details"	: ``(WirelessWpaEapDetailsDialog()),
	"commit"	: [ ``(Commit()), true ],
    ];

    string ws_start = which == "wire"? "wire": "address"; //"changedefaults";
    map sequence = $[
	"ws_start"	: ws_start,
/*
	"changedefaults" : $[
	    `next	: "address",
	],
*/
	"address" : $[
	    `abort	: `abort,
	    `next	: "commit",
	    `wire	: "wire",
	    `hosts	: "hosts",
	    `s390	: "s390",
	    `hardware	: `hardware,
	],
	"s390" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
	"hosts" : $[
	    `abort	: `abort,
	    `next	: "address",
	],
	"wire" : $[
	    `next	: "commit",
	    `expert	: "expert",
	    `keys	: "keys",
	    `eap	: "eap",
	    `abort	: `abort,
	],
	"expert" : $[
	    `next	: "wire",
	    `abort	: `abort,
	],
	"keys" : $[
	    `next	: "wire",
	    `abort	: `abort,
	],
	"eap" : $[
	    `next	: "commit",
	    `details	: "eap-details",
	    `abort	: `abort,
	],
	"eap-details" : $[
	    `next	: "eap",
	    `abort	: `abort,
	],
	"commit" : $[
	    `next	: `next,
	],
    ];

    return Sequencer::Run(aliases, sequence);
}

/* EOF */
}
