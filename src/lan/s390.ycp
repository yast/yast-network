/**
 * File:        include/network/lan/s390.ycp
 * Package:     Network configuration
 * Summary:     Network card adresss configuration dialogs
 * Authors:     Michal Filka <mfilka@suse.cz>
 *
 * Functions for accessing and handling s390 specific needs.
 */

import "FileUtils";

include "network/routines.ycp";

const string sys_dir = "/sys/class/net";

/**
 * Checks if driver was successfully loaded for particular device.
 */
define boolean s390_DriverLoaded( string devname)
{
    if( !Arch::s390())
        return false;

    if( IsEmpty( devname))
        return false;

    return FileUtils::IsDirectory( sformat( "%1/%2", sys_dir, devname)) == true;
}

/**
 * Reads particular qeth attribute and returns its value as a string.
 *
 * @param attrib attribute name as exported by qeth module
 * @return attribute value or nil in case of error.
 */
define string s390_ReadQethAttribute( string devname, string attrib)
{
    if( !s390_DriverLoaded( devname))
        return nil;
    
    string result = (string) SCR::Read( .target.string, sformat( "%1/%2/device/%3", sys_dir, devname, attrib));

    return regexpsub( result, "(.*)\n", "\\1");
}

/**
 * Reads attributes for particular qeth based network device.
 *
 * Returned map is compatible with similar map used for storing sysconfig values used elswhere in the code.
 * As a consequence, boolean values are stored as strings with yes/no value.
 *
 * Currently loaded attributes are:
 * QETH_LAYER2      yes/no string.
 * QETH_PORTNAME    portname or empty string
 * QETH_PORTNUMBER  portnumber or empty string
 * QETH_CHANIDS     read/write/control channel ids separated by space (compatibility requirement)
 *
 * see lsqeth for inspiration
 *
 * @return a map with keys QETH_LAYER2, QETH_PORTNAME, QETH_PORTNUMBER, QETH_CHANIDS
 */
define map<string, any> s390_ReadQethConfig( string devname)
{
    if( !s390_DriverLoaded( devname))
        return $[];
    
    map<string, any> result = $[];

    string qeth_layer2 = s390_ReadQethAttribute( devname, "layer2") == "1" ? "yes" : "no";
    result = add( result, "QETH_LAYER2", qeth_layer2);

    string qeth_portname = s390_ReadQethAttribute( devname, "portname");
    result = add( result, "QETH_PORTNAME", qeth_portname);

    string qeth_portno = s390_ReadQethAttribute( devname, "portno");
    result = add( result, "QETH_PORTNUMBER", qeth_portno);

    // FIXME: another code handles chanids merged in one string separated by spaces.
    string read_chan = s390_ReadQethAttribute( devname, "cdev0");
    string write_chan = s390_ReadQethAttribute( devname, "cdev1");
    string ctrl_chan = s390_ReadQethAttribute( devname, "cdev2");
    string qeth_chanids = mergestring( [ read_chan, write_chan, ctrl_chan ], " ");
    result = add( result, "QETH_CHANIDS", qeth_chanids);

    // TODO: ipa_takover. study a bit. It cannot be read from /sys. Not visible using lsqeth,
    // qethconf configures it.

    y2debug( "s390_ReadQethConfig: %1", result);

    return result;
}
