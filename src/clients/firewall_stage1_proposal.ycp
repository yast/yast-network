/* ------------------------------------------------------------------------------
 * Copyright (c) 2008 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	firewall_stage1_proposal.ycp
 * Summary:	Configuration of fw in 1st stage
 * Author:	Bubli <kmachalkova@suse.cz>
 *
 */

{
    textdomain "network";

    import "Label";
    import "Linuxrc";
    import "PackagesProposal";
    import "ProductControl";
    import "ProductFeatures";
    import "SuSEFirewall4Network";
    import "SuSEFirewallProposal";
    import "Wizard";

    string proposal_id = "fw_1ststage";

    symbol FirewallDialogSimple() {
	string title =  _("Basic Firewall and SSH Configuration");

	term contents = `VBox (
            `Frame (
            // frame label
            _("Firewall and SSH service"),
            `HSquash (
                `MarginBox (
                        0.5, 0.5,
			`VBox(
			    `Left(`CheckBox(`id("enable_fw"),`opt(`notify), 
				    _("Enable Firewall"), SuSEFirewall4Network::Enabled1stStage())),
			    `Left(`CheckBox(`id("open_port"), 
				    _("Open SSH Port and Enable SSH Service"), SuSEFirewall4Network::EnabledSsh1stStage()))
			)
                    )
                )
            )
	);

	string help = _("<p><b><big>Firewall and SSH</big></b><br>
Firewall is a defensive mechanism that protects your computer from network attacks.
SSH is a service that allows logging into this computer remotely via dedicated
SSH client</p>") +

_("<p>Here you can choose whether the firewall will be enabled or disabled after
the installation. It is recommended to keep it enabled.</p>") +

_("<p>With enabled firewall, you can decide whether to open firewall port for SSH
service and allow remote SSH logins. This will also enable SSH service (i.e. it
will be started on computer boot).</p>");


	Wizard::CreateDialog ();
	Wizard::SetTitleIcon ("yast-firewall");

	Wizard::SetContentsButtons (title, contents, help, Label::BackButton(), Label::OKButton());
	Wizard::SetAbortButton (`cancel, Label::CancelButton());
	Wizard::HideBackButton();

	UI::ChangeWidget(`id("open_port"),`Enabled, SuSEFirewall4Network::Enabled1stStage());

	any dialog_ret = nil;

	while (true) {
	    dialog_ret =  UI::UserInput();
	    boolean enable = (boolean) UI::QueryWidget(`id("enable_fw"),`Value);

	    if (dialog_ret == "enable_fw") {
		UI::ChangeWidget(`id("open_port"),`Enabled, enable);
		continue;
	    }
	    else if( dialog_ret == `next || dialog_ret == `ok) {
	        boolean open = (boolean) UI::QueryWidget(`id("open_port"),`Value);
	        boolean can_be_opened = (boolean) UI::QueryWidget(`id("open_port"),`Enabled);

		SuSEFirewall4Network::SetEnabled1stStage(enable);
		if (can_be_opened)
		    SuSEFirewall4Network::SetSshEnabled1stStage(open);
		break;
	    }
	    else
		break;
	}

	Wizard::CloseDialog();
        return (symbol) dialog_ret;

    }

    // run this only once
    if (!SuSEFirewallProposal::GetProposalInitialized()) {

        // variables from control file
        y2milestone("Default firewall values: enable_firewall=%1, enable_ssh=%2",
            ProductFeatures::GetBooleanFeature ("globals", "enable_firewall"), 
	    ProductFeatures::GetBooleanFeature ("globals", "firewall_enable_ssh"));

        SuSEFirewall4Network::SetEnabled1stStage(ProductFeatures::GetBooleanFeature ("globals", "enable_firewall"));

	//we're installing over SSH, propose opening SSH port (bnc#535206)
	if(Linuxrc::usessh()) {
	    SuSEFirewall4Network::SetSshEnabled1stStage (true);
	} else {
	    SuSEFirewall4Network::SetSshEnabled1stStage (ProductFeatures::GetBooleanFeature ("globals", "firewall_enable_ssh"));
	}

        SuSEFirewall4Network::SetSshEnabled1stStage (ProductFeatures::GetBooleanFeature ("globals", "firewall_enable_ssh"));

        SuSEFirewallProposal::SetProposalInitialized(true);
    }


    string func  = (string)WFM::Args(0);
    map    param = (map)WFM::Args(1);
    map<string,any>    ret   = $[];


    if ( func == "MakeProposal" )
    {
	string output = "<ul>\n";
        output = output + "<li>" + (SuSEFirewall4Network::Enabled1stStage() ?
            _("Firewall will be enabled (<a href=\"firewall--disable_firewall_in_proposal\">disable</a>)")
            :
            _("Firewall will be disabled (<a href=\"firewall--enable_firewall_in_proposal\">enable</a>)")
        ) + "</li>\n";

	if (SuSEFirewall4Network::Enabled1stStage())
	{
	    output = output + "<li>" + (SuSEFirewall4Network::EnabledSsh1stStage() ?
                _("SSH service will be enabled, SSH port will be open (<a href=\"firewall--disable_ssh_in_proposal\">disable and close</a>)")
                :
                _("SSH service will be disabled, SSH port will be blocked (<a href=\"firewall--enable_ssh_in_proposal\">enable and open</a>)")
           ) + "</li>\n";

	}
        output = output + "</ul>\n";

	ret = $[
        "preformatted_proposal" : output,
        "warning_level" : `warning,
        //"warning" : proposal["warning"]:nil,
        "links" : [
            "firewall--enable_firewall_in_proposal", "firewall--disable_firewall_in_proposal",
            "firewall--enable_ssh_in_proposal", "firewall--disable_ssh_in_proposal",
        ]
    ];

    }
    else if ( func == "AskUser" ) {
	any chosen_link = param["chosen_id"]:nil;
	symbol result = `next;
	y2milestone("User clicked %1", chosen_link);

	if (chosen_link == "firewall--enable_firewall_in_proposal")
	{
	    y2milestone("Enabling FW");
	    SuSEFirewall4Network::SetEnabled1stStage(true);
	    PackagesProposal::AddResolvables(proposal_id, `package, ["SuSEfirewall2"]);
	}
	else if (chosen_link == "firewall--disable_firewall_in_proposal")
	{
	    y2milestone("Disabling FW");
	    SuSEFirewall4Network::SetEnabled1stStage( false );
	}
	else if (chosen_link == "firewall--enable_ssh_in_proposal")
	{
	    y2milestone("Enabling SSH");
	    PackagesProposal::AddResolvables(proposal_id, `package, ["openssh"]);
	    SuSEFirewall4Network::SetSshEnabled1stStage( true );
	}
	else if (chosen_link == "firewall--disable_ssh_in_proposal")
	{
	    y2milestone("Disabling SSH");
	    SuSEFirewall4Network::SetSshEnabled1stStage( false );
	}
	else if (chosen_link == "firewall_stage1")
	{
	    result = FirewallDialogSimple();
	}

	SuSEFirewallProposal::SetChangedByUser(true);

	//Disable one of the AC steps (see control.$product.xml for appropriate unique IDs)
	if ( ProductControl::GetUseAutomaticConfiguration() )
	    ProductControl::DisableACItem("ac_2", "firewall");
	//or subproposal (if 2nd stage is non-automatic)
	else
	    ProductControl::DisableSubProposal("network_continue", "firewall");

	ret = $[ "workflow_sequence" : result ];
    }
    else if ( func == "Description" )
    {
        ret =
            $[
                // Proposal title 
              "rich_text_title" :       _("Firewall and SSH"),
                // Menu entry label
              "menu_title"      :       _("&Firewall and SSH"),
              "id"              :       "firewall_stage1"
            ];
    }
    else if (func == "Write")
    {
	ret =
	    $[
		"success"	:	true
	    ];
    }

    return ret;
}
