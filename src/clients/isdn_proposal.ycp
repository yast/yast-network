/****************************************************************************
 *
 * Copyright (c) 2012 Novell, Inc.
 * All Rights Reserved.
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of version 2 of the GNU General Public License as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail,
 * you may find current contact information at www.novell.com
 *
 ****************************************************************************/
/**
 * File:	clients/isdn_proposal.ycp
 * Package:	Configuration of network
 * Summary:	ISDN configuration proposal
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 */

{

textdomain "network";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("ISDN proposal started");
y2milestone("Arguments: %1", WFM::Args());

import "ISDN";
import "Progress";
import "GetInstArgs";

include "network/routines.ycp";

string func = (string) WFM::Args(0);
map param = (map) WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    string proposal = "";
    string warning = nil;
    symbol warning_level = nil;
    boolean force_reset = param["force_reset"]:false;

    if(force_reset || !ISDN::proposal_valid) {
	ISDN::proposal_valid = true;
	if (! GetInstArgs::automatic_configuration())
	    /* Popup text */
	    BusyPopup(_("Detecting ISDN cards..."));
	boolean progress_orig = Progress::set (false);
	ISDN::Read();
	// no ISDN::Propose () ?
	Progress::set (progress_orig);
	if (! GetInstArgs::automatic_configuration())
	    BusyPopupClose();
    }
    list sum = ISDN::Summary(false);
    proposal = sum[0]:"";

    ret = $[
	"preformatted_proposal" : proposal,
	"warning_level" : warning_level,
	"warning" : warning,
    ];
}
/* run the module */
else if(func == "AskUser") {
    map stored = ISDN::Export();
    any seq = WFM::CallFunction("isdn", [.propose]);
    if(seq != `next) ISDN::Import(stored);
    ret = $[
	"workflow_sequence" : seq
    ];
}
/* create titles */
else if(func == "Description") {
    ret = $[
	/* RichText label */
	"rich_text_title" : _("ISDN Adapters"),
	/* Menu label */
	"menu_title" : _("&ISDN Adapters"),
	"id" : "isdn",
    ];
}
/* write the proposal */
else if(func == "Write") {
    ISDN::Write (true);		// #74096, full init before internet test
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("ISDN proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
